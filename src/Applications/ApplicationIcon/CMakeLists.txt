cmake_minimum_required(VERSION 3.5)

# Project
project(ApplicationIcon)

find_package(wxWidgets REQUIRED)
include(${wxWidgets_USE_FILE})

set(SOURCES README.md ApplicationIcon.cpp)

# Resources
if (MSVC)
  set(SOURCES README.md ${SOURCES} ${CMAKE_CURRENT_BINARY_DIR}/resources/${PROJECT_NAME}.rc)
  set(RESOURCES Resources/Gammasoft.ico)
  get_filename_component(ABSOLUTE_PATH_FILE_ICON "${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.ico" REALPATH BASE_DIR "${CMAKE_CURRENT_SOURCE_DIR}")
  string(REPLACE "/" "\\\\" ABSOLUTE_PATH_FILE_ICON ${ABSOLUTE_PATH_FILE_ICON})
  file(WRITE ${CMAKE_CURRENT_BINARY_DIR}/resources/${PROJECT_NAME}.rc
    "#pragma region CMake generated code\n"
    "// This code was generated by CMake script.\n"
    "//\n"
    "// Changes to this file may cause incorrect behavior and will be lost if the code is regenerated.\n"
    "\n"
    "#include <Windows.h>\n"
    "\n"
    "LANGUAGE LANG_NEUTRAL, SUBLANG_NEUTRAL\n"
    "#pragma code_page(65001)\n"
    "\n"
    "IDI_APPLICATION ICON DISCARDABLE \"${ABSOLUTE_PATH_FILE_ICON}\"\n\n"
    "VS_VERSION_INFO VERSIONINFO\n"
    " FILEVERSION 1,0,0,0\n"
    " PRODUCTVERSION 1,0,0\n"
    " FILEFLAGSMASK 0x0L\n"
    "#if !defined(NDEBUG)\n"
    " FILEFLAGS VS_FF_DEBUG\n"
    "#endif\n"
    " FILEOS VOS_NT\n"
    " FILETYPE VFT_APP\n"
    " FILESUBTYPE VFT2_UNKNOWN\n"
    "BEGIN\n"
    "  BLOCK \"StringFileInfo\"\n"
    "  BEGIN\n"
    "    BLOCK \"000004b0\"\n"
    "    BEGIN\n"
    "      VALUE \"CompanyName\", \"Gammasoft\"\n"
    "      VALUE \"FileDescription\", \"${PROJECT_NAME} example\"\n"
    "      VALUE \"FileVersion\", \"1,0,0,0\"\n"
    "      VALUE \"InternalName\", \"${PROJECT_NAME}\"\n"
    "      VALUE \"LegalCopyright\", \"\"\n"
    "      VALUE \"LegalTrademarks1\", \"\"\n"
    "      VALUE \"LegalTrademarks2\", \"\"\n"
    "      VALUE \"OriginalFilename\", \"${PROJECT_NAME}.exe\"\n"
    "      VALUE \"ProductName\", \"\"\n"
    "      VALUE \"ProductVersion\", \"1,0,0\"\n"
    "    END\n"
    "  END\n"
    "  BLOCK \"VarFileInfo\"\n"
    "  BEGIN\n"
    "    VALUE \"Translation\", 0x0, 1200\n"
    "  END\n"
    "END\n"
    "#pragma endregion\n"
  )
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.ico ${CMAKE_CURRENT_BINARY_DIR}/Resources/Gammasoft.ico COPYONLY)
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.ico ${CMAKE_CURRENT_BINARY_DIR}/Debug/Resources/Gammasoft.ico COPYONLY)
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.ico ${CMAKE_CURRENT_BINARY_DIR}/Release/Resources/Gammasoft.ico COPYONLY)
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.png ${CMAKE_CURRENT_BINARY_DIR}/Resources/Gammasoft.png COPYONLY)
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.png ${CMAKE_CURRENT_BINARY_DIR}/Debug/Resources/Gammasoft.png COPYONLY)
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.png ${CMAKE_CURRENT_BINARY_DIR}/Release/Resources/Gammasoft.png COPYONLY)
elseif (APPLE)
  set(RESOURCES Resources/Gammasoft.icns Resources/Gammasoft.png)
  set_source_files_properties(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.icns PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")
  set_source_files_properties(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.png PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")
  set(MACOSX_BUNDLE_ICON_FILE "Gammasoft.icns")
elseif (UNIX)
  set(RESOURCES Resources/Gammasoft.png)
  file(WRITE ${CMAKE_CURRENT_BINARY_DIR}/resources/${PROJECT_NAME}.desktop
    "[Desktop Entry]\n"
    "Type=Application\n"
    "Encoding=UTF-8\n"
    "Comment=${PROJECT_NAME} example\n"
    "Exec=${CMAKE_CURRENT_BINARY_DIR}/${PROJECT_NAME}\n"
    "Icon=Gammasoft.png\n"
    "Name=${PROJECT_NAME}\n"
    "NoDisplay=true\n"
    "Terminal=false\n"
    "Version=1.0.0\n"
  )
  configure_file(${CMAKE_CURRENT_SOURCE_DIR}/Resources/Gammasoft.png $ENV{HOME}/.local/share/icons/hicolor/512x512/apps/Gammasoft.png COPYONLY)
  file(COPY ${CMAKE_CURRENT_BINARY_DIR}/resources/${PROJECT_NAME}.desktop DESTINATION "$ENV{HOME}/.local/share/applications" FILE_PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ)
  configure_file(${CMAKE_CURRENT_BINARY_DIR}/resources/${PROJECT_NAME}.desktop $ENV{HOME}/.local/share/applications/${PROJECT_NAME}.desktop COPYONLY)
endif ()

# Options
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Target
add_executable(${PROJECT_NAME} WIN32 MACOSX_BUNDLE ${SOURCES} ${RESOURCES})
target_link_libraries(${PROJECT_NAME} ${wxWidgets_LIBRARIES})
set_target_properties(${PROJECT_NAME} PROPERTIES FOLDER "Applications")
